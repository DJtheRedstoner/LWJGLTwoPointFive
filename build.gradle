import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "com.github.asbyth:ForgeGradle:ddb1eb01af"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"

version = "1.0-SNAPSHOT"
group= "me.djtheredstoner"
archivesBaseName = "LWJGLTwoPointFive"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    main {
        output.resourcesDir = java.outputDir
    }
}

configurations {
    lwjgl
    lwjglNative {
        transitive = false
    }
}

sourceSets.main.runtimeClasspath += configurations.lwjglNative

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    mappings = "stable_22"

    makeObfSourceJar = false

    clientJvmArgs += "-Dfml.coreMods.load=me.djtheredstoner.lwjgl.plugin.LoadingPlugin"
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

task lwjglJar(type: ShadowJar) {
    group = "shadow"
    archiveClassifier.set("lwjgl")
    configurations = [project.configurations.lwjgl]
    exclude "META-INF/versions/**"
    exclude "**/module-info.class"
    exclude "**/package-info.class"
    relocate("org.lwjgl", "org.lwjgl3") {
        include "org.lwjgl.PointerBuffer"
        include "org.lwjgl.BufferUtils"
    }
}

dependencies {
    lwjgl "org.lwjgl:lwjgl:3.3.0"
    lwjgl "org.lwjgl:lwjgl-tinyfd:3.3.0"
    lwjgl "org.lwjgl:lwjgl-nanovg:3.3.0"
    lwjglNative "org.lwjgl:lwjgl:3.3.0:natives-windows"
    lwjglNative "org.lwjgl:lwjgl-tinyfd:3.3.0:natives-windows"
    lwjglNative "org.lwjgl:lwjgl-nanovg:3.3.0:natives-windows"
    implementation lwjglJar.outputs.files
}

jar { enabled = false }
reobfJar { enabled = false }

shadowJar {
    archiveClassifier.set("")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.attributes(
        "FMLCorePlugin": "me.djtheredstoner.lwjgl.plugin.LoadingPlugin",
        "FMLCorePluginContainsFMLMod": "No (but actually yes)"
    )
    configurations += [project.configurations.lwjglNative]
}

reobf {
    shadowJar {}
}

build.dependsOn shadowJar

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
